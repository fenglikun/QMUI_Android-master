apply plugin: 'com.android.application'

def getVersion() {
  def gitVersion
  try {
	def cmd = 'git rev-list HEAD --count'
	gitVersion = cmd.execute().text.trim().toInteger()
  } catch (Exception e) {
	gitVersion = 1
  }
  return gitVersion
}

def gitVersion = getVersion()
def javaVersion = JavaVersion.VERSION_1_8

android {
    signingConfigs {
        Properties properties = new Properties()
        File propFile = project.file('release.properties')
        if (propFile.exists()) {
            properties.load(propFile.newDataInputStream())
        }
        release {
            keyAlias properties.getProperty("RELEASE_KEY_ALIAS")
            keyPassword properties.getProperty("RELEASE_KEY_PASSWORD")
            storeFile file('qmuidemo.keystore')
            storePassword properties.getProperty("RELEASE_STORE_PASSWORD")
            v2SigningEnabled false
        }
    }
    compileSdkVersion parent.ext.compileSdkVersion
    buildToolsVersion parent.ext.buildToolsVersion

    // for butterknife, see https://github.com/JakeWharton/butterknife/blob/master/CHANGELOG.md#version-900-rc2-2018-11-19
    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }

    defaultConfig {
        applicationId "com.qmuiteam.qmuidemo"
        minSdkVersion parent.ext.minSdkVersion
        targetSdkVersion parent.ext.targetSdkVersion
        versionCode gitVersion
        versionName "1.2.0"
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    // 避免 lint 检测出错时停止构建
    lintOptions {
      abortOnError false
    }
}

//apply plugin: 'com.qmuiteam.qmui'
//qmui {
//    parentTheme "AppRootTheme"
//}


// 加@aar与不加@aar的区别：
// http://stackoverflow.com/questions/30157575/why-should-i-include-a-gradle-dependency-as-aar
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"
//    implementation 'com.qmuiteam:qmuilint:1.0.1'
//    implementation 'com.qmuiteam:arch:0.3.1'
//    implementation 'com.qmuiteam:qmui:1.2.0'
    implementation project(':lib')
    implementation project(':qmuilint')
    implementation project(':qmui')
    implementation project(':arch')
    annotationProcessor project(':compiler')

    //leak
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
}
